# Special Pythagorean triplet

## Problem

A *Pythagorean triplet* is a set of three natural numbers, \( a < b < c \), for which, \( a^2 + b^2 = c^2 \).
For example, \( 3^2 + 4^2 = 9 + 16 = 25 = 5^2 \).

There exists exactly one Pythagorean triplet for which \( a + b + c = 1000 \).
Find the product \( abc \).


## Methodology

Let \( p \) be the given perimeter of the triangle. We parameterize the sides as \( a = m^2 - n^2, b = 2mn, c = m^2 + n^2 \), where \( m > n > 0 \). Using the fact that \( a + b + c = p \), we get \( 2 m (m + n) = p \).

Now we need good bounds for \( m, n \) in order to reduce the search space. Note that for \( n \) to be maximum, we must have \( m = n + 1 \), so we get the estimate \( n ≤ \frac{\sqrt{p}}{2} \). On the other hand, for \( m \) to maximum, we require \( n = 1 \), so we get the estimate \( m ≤ \sqrt{\frac{p}{2}} \).

Finally, the required product after simplification is \( p n (m - n) (m^2 + n^2) \).


## Code

Now the code is just a single line of computation.

```julia
function special_Pythagorean_triplet(perimeter::Int)::Int
    for n ∈ 1:(isqrt(perimeter) ÷ 2), m ∈ n:isqrt(perimeter ÷ 2)
        if 2m * (m + n) == perimeter
            return perimeter * n * (m - n) * (m^2 + n^2)
        end
    end
    return 0
end

@time println(special_Pythagorean_triplet(1000))
```


## Solution
\( 31875000 \)


## References

1.  [The problem overview](https://projecteuler.net/overview=009).
2.  Pier's forum post on Thu, 19 Aug 2004, 09:04.
